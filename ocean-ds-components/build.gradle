apply plugin: 'maven-publish'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.kezong.fat-aar'

def versionNameProp = ""

task loadVersionName () {
    def VERSION_NAME = 'VERSION_NAME'

    Properties props = new Properties()
    def propFile = file('version.properties')

    if (propFile.canRead()){
        props.load(new FileInputStream(propFile))

        if (props != null && props.containsKey(VERSION_NAME)) {
            versionNameProp = props[VERSION_NAME].toString()
        }
    }
}

android {
    compileSdkVersion rootProject.ext.compileSDK
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSDK
        targetSdkVersion rootProject.ext.targetSDK
        versionCode 1
        versionName versionNameProp.toString()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation 'androidx.core:core-ktx:1.3.0'
    api 'androidx.appcompat:appcompat:1.1.0'
    api 'androidx.constraintlayout:constraintlayout:1.1.3'

    embed project(path: ':ocean-ds-tokens')

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

afterEvaluate {
    publishing {
        def gprBaseUrl = "https://maven.pkg.github.com"
        def gprRepoOwner = "Pagnet"
        def gprRepoId = "ocean-ds-android"

        def gprUser = System.getenv("GPR_USER")
        def gprApiKey = System.getenv("GPR_API_KEY")

        repositories {
            maven {
                name = "ocean-ds-android"
                url = uri("$gprBaseUrl/$gprRepoOwner/$gprRepoId")
                credentials {
                    username = gprUser
                    password = gprApiKey
                }
            }
        }

        def mavenGroupId = "br.com.useblu"
        def mavenArtifactId = project.name

        publications {
            release(MavenPublication) {
                from components.release
                groupId = mavenGroupId
                artifactId = mavenArtifactId
                version = versionNameProp
            }
        }
    }
}